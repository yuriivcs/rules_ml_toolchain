# Copyright 2025 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ==============================================================================

# ==============================================================================
# Tools: Linux x86_64, Sysroot: Linux x86_64, CUDA
# ==============================================================================

load("@local_config_cuda//cuda:build_defs.bzl", "if_cuda", "if_cuda_hermetic_clang")
load("@rules_cc//cc:defs.bzl", "cc_toolchain")
load("//cc/sanitizers:features.bzl",
    "cc_toolchain_import_asan_feature",
    "cc_toolchain_import_asan_runtime_closure_feature")
load("//third_party/rules_cc_toolchain:toolchain_config.bzl", "cc_toolchain_config")
load("//third_party/rules_cc_toolchain/features:cc_toolchain_import.bzl", "cc_toolchain_import")
load("//third_party/rules_cc_toolchain/features:features.bzl", "cc_toolchain_import_feature")

package(
    default_visibility = [
        "//cc:__pkg__",
    ],
)

CUDA_TOOLS = if_cuda_hermetic_clang(
    {
        "gcc": "wrappers/clang",
        "cpp": "wrappers/clang++",
        "ld": "wrappers/ld",
        "ar": "wrappers/ar",
        "gcov": "wrappers/idler",
        "llvm-cov": "wrappers/idler",
        "nm": "wrappers/idler",
        "objdump": "wrappers/idler",
        "strip": "wrappers/idler",
    },
    {
        "gcc": "wrappers/nvcc_wrapper",
        "cpp": "wrappers/clang++",
        "ld": "wrappers/ld",
        "ar": "wrappers/ar",
        "gcov": "wrappers/idler",
        "llvm-cov": "wrappers/idler",
        "nm": "wrappers/idler",
        "objdump": "wrappers/idler",
        "strip": "wrappers/idler",
    },
)

filegroup(
    name = "wrappers",
    srcs = [
        "//cc/impls/linux_x86_64_linux_x86_64_cuda/wrappers:all",
    ],
    visibility = ["//visibility:public"],
)

# TODO(ybaturina): rename to "cuda_bin"
filegroup(
    name = "cuda_nvcc",
    srcs = if_cuda([
        "@cuda_nvcc//:bin",
        "@cuda_nvcc//:nvvm",
        "@cuda_nvvm//:cicc",
    ]),
)

alias(
    name = "cuda_nvcc_feature",
    actual = "@cuda_nvcc//:feature",
    visibility = ["//visibility:public"],
)

alias(
    name = "startup_libs",
    actual = "@sysroot_linux_x86_64//:startup_libs",
    visibility = ["//visibility:public"],
)

cc_toolchain_import_feature(
    name = "startup_libs_feature",
    enabled = True,
    toolchain_import = ":startup_libs",
)

# buildifier: leave-alone
cc_toolchain_import(
    name = "imports",
    deps = [
        "@rules_ml_toolchain//cc/cuda/clang:inject_cuda_runtime_wrapper",
        "@sysroot_linux_x86_64//:includes_c",
        "@llvm_linux_x86_64//:includes",
        "@sysroot_linux_x86_64//:includes_system",
        "@sysroot_linux_x86_64//:syslibs",
        "@sysroot_linux_x86_64//:openmp",
        "@llvm_linux_x86_64//:libclang_rt",
    ],
    visibility = ["//visibility:public"],
)

cc_toolchain_import_feature(
    name = "imports_feature",
    enabled = True,
    toolchain_import = ":imports",
)

cc_toolchain_import(
    name = "asan",
    deps = [
        "@llvm_linux_x86_64//:libclang_rt",
        "@llvm_linux_x86_64//:rt_asan",
    ],
)

# buildifier: leave-alone
filegroup(
    name = "all",
    srcs = [
        ":cuda_nvcc",
        ":imports",
        ":wrappers",
        ":startup_libs",
        "@llvm_linux_x86_64//:all",
    ],
)

# buildifier: leave-alone
filegroup(
    name = "compiler",
    srcs = [
        ":asan",
        ":cuda_nvcc",
        ":imports",
        ":wrappers",
        "@llvm_linux_x86_64//:clang",
        "@llvm_linux_x86_64//:clang++",
        "@llvm_linux_x86_64//:distro_libs",
        "@llvm_linux_x86_64//:asan_ignorelist",
    ],
)

# buildifier: leave-alone
filegroup(
    name = "linker",
    srcs = [
        ":compiler",
        ":cuda_nvcc",
        ":wrappers",
        "@llvm_linux_x86_64//:ld",
        ":startup_libs",
        "@llvm_linux_x86_64//:distro_libs",
    ],
)

# buildifier: leave-alone
filegroup(
    name = "ar",
    srcs = [
        ":cuda_nvcc",
        ":wrappers",
        "@llvm_linux_x86_64//:ar",
        "@llvm_linux_x86_64//:distro_libs",
    ],
)

# buildifier: leave-alone
filegroup(
    name = "objcopy",
    srcs = [
        ":wrappers",
        "@llvm_linux_x86_64//:objcopy",
        "@llvm_linux_x86_64//:distro_libs",
    ],
)

# buildifier: leave-alone
filegroup(
    name = "strip",
    srcs = [
        ":wrappers",
        "@llvm_linux_x86_64//:strip",
        "@llvm_linux_x86_64//:distro_libs",
    ],
)

FEATURES_ESSENTIAL = [
    ":imports_feature",
    ":cuda_nvcc_feature",
    "//cc/cuda/features:hermetic",
    "//third_party/rules_cc_toolchain/features:hermetic_libraries",

    # Toolchain configuration
    "//third_party/rules_cc_toolchain/features:warnings",
    "//third_party/rules_cc_toolchain/features:errors",
    "//third_party/rules_cc_toolchain/features:reproducible",
    "//third_party/rules_cc_toolchain/features:use_lld",
    "//cc/features:sysroot_linux_x86_64",
    ":startup_libs_feature",
    "//third_party/rules_cc_toolchain/features:coverage",
    #"//cc/features:clang19",    # TODO: Add a selection mechanism based on the Clang version

    # PIC / PIE flags
    "//third_party/rules_cc_toolchain/features:supports_pic",
    "//third_party/rules_cc_toolchain/features:position_independent_code",
    "//third_party/rules_cc_toolchain/features:position_independent_executable",

    # Optimization flags
    "//third_party/rules_cc_toolchain/features:garbage_collect_symbols",
    "//cc/features:constants_merge",
    "//cc/features:asserts_disable",
    "//third_party/rules_cc_toolchain/features:opt",
    "//cc/features:detect_issues",
    #"//cc/features:detect_issues_ext",   # Non-hermetic builds don't include these flags

    # C++ standard flags
    "//third_party/rules_cc_toolchain/features:c++11",
    "//third_party/rules_cc_toolchain/features:c++14",
    "//third_party/rules_cc_toolchain/features:c++17",

    # Other flags
    "//cc/features:allow_shlib_undefined",
    "//cc/features:supports_dynamic_linker",
    "//cc/features:supports_start_end_lib_feature",
]

cc_toolchain_config(
    name = "config",
    archiver = "@llvm_linux_x86_64//:ar",
    c_compiler = "@llvm_linux_x86_64//:clang",
    cc_compiler = "@llvm_linux_x86_64//:clang++",
    compiler_features = FEATURES_ESSENTIAL + [
        "//third_party/rules_cc_toolchain/features:dbg_off",
    ],
    linker = "@llvm_linux_x86_64//:ld",
    strip_tool = "@llvm_linux_x86_64//:strip",
    target_cpu = "x86_64",
    target_system_name = "local",
    tool_paths = CUDA_TOOLS,
)

cc_toolchain(
    name = "toolchain",
    all_files = ":all",
    ar_files = ":ar",
    compiler_files = ":compiler",
    dwp_files = ":all",
    linker_files = ":linker",
    objcopy_files = ":objcopy",
    strip_files = ":strip",
    supports_param_files = 1,
    toolchain_config = ":config",
    toolchain_identifier = "toolchain_linux_x86_64_linux_x86_64_id",
)

# Toolchain with sanitizers

# Separate tools for additional diagnostics; can be removed in the future
CUDA_WITH_SANITIZER_TOOLS = if_cuda_hermetic_clang(
    {
        "gcc": "wrappers/clang-with-sanitizer",
        "cpp": "wrappers/clang++",
        "ld": "wrappers/ld",
        "ar": "wrappers/ar",
        "gcov": "wrappers/idler",
        "llvm-cov": "wrappers/idler",
        "nm": "wrappers/idler",
        "objdump": "wrappers/idler",
        "strip": "wrappers/idler",
    },
    {
        "gcc": "wrappers/nvcc_wrapper",
        "cpp": "wrappers/clang++",
        "ld": "wrappers/ld",
        "ar": "wrappers/ar",
        "gcov": "wrappers/idler",
        "llvm-cov": "wrappers/idler",
        "nm": "wrappers/idler",
        "objdump": "wrappers/idler",
        "strip": "wrappers/idler",
    },
)

cc_toolchain_import_asan_feature(
    name = "asan_feature",
    enabled = select({
        "//common:is_asan_enabled": True,
        "//conditions:default": False,
    }),
    toolchain_import = ":asan",
)

cc_toolchain_import_asan_runtime_closure_feature(
    name = "asan_runtime_closure_feature",
    requires = ["asan_feature"],
    toolchain_import = ":asan",
)

cc_toolchain_config(
    name = "config_with_sanitizers",
    archiver = "@llvm_linux_x86_64//:ar",
    c_compiler = "@llvm_linux_x86_64//:clang",
    cc_compiler = "@llvm_linux_x86_64//:clang++",
    compiler_features = FEATURES_ESSENTIAL + [
        ":asan_feature",
        ":asan_runtime_closure_feature",
        "//third_party/rules_cc_toolchain/features:dbg",
    ],
    linker = "@llvm_linux_x86_64//:ld",
    strip_tool = "@llvm_linux_x86_64//:strip",
    target_cpu = "x86_64",
    target_system_name = "local",
    tool_paths = CUDA_WITH_SANITIZER_TOOLS,
)

cc_toolchain(
    name = "toolchain_with_sanitizers",
    all_files = ":all",
    ar_files = ":ar",
    compiler_files = ":compiler",
    dwp_files = ":all",
    linker_files = ":linker",
    objcopy_files = ":objcopy",
    strip_files = ":strip",
    supports_param_files = 1,
    toolchain_config = ":config_with_sanitizers",
    toolchain_identifier = "toolchain_linux_x86_64_linux_x86_64_with_sanitizers_id",
)
